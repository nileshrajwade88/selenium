'# ********************************************************************************************
'  Define global variables
'# ********************************************************************************************
Public g_dict_sheetheaders


'# ********************************************************************************************
'# Function/Sub Name        : func_testdata_TCHeaders
'# Purpose                  : to get all data sheet headers to create dictionary objects with this header
'# Input Parameters         : None
'# Output Parameters        : dictionary object with all headers (sheet name and header name)
'# Author			        : Kiran Londhe
'# Date Created[dd-mon-yy]  : 16-Jan-2017
'# Modified By				:
'# Date Modified[dd-mon-yy] :
'# ********************************************************************************************

Public Function func_testdata_TCHeaders()
Dim l_dict_sheetparams
Dim l_int_sheetsct
Dim l_int_iteratorsheet
Dim l_int_iteratorcols
Dim l_str_sheetname
Dim l_str_headers
Dim l_obj_sheet
Dim l_int_headercount
Dim l_arr_headers


set l_dict_sheetparams =CreateObject("Scripting.Dictionary")
	l_int_sheetsct = DataTable.GetSheetCount 
	For l_int_iteratorsheet =1 to l_int_sheetsct
		l_str_sheetname= DataTable.GetSheet(l_int_iteratorsheet).Name
		l_str_headers = ""		
		Set l_obj_sheet = DataTable.GetSheet(l_str_sheetname)
		l_int_headercount = l_obj_sheet.GetParameterCount				
			For l_int_iteratorcols = 1 to l_int_headercount
			    l_str_headername = l_obj_sheet.GetParameter(l_int_iteratorcols).Name							
					If len(l_str_headername) > 2 Then
						If l_str_headers = "" Then
						   l_str_headers = l_obj_sheet.GetParameter(l_int_iteratorcols).Name
						Else
						   l_str_headers = l_str_headers & "," & l_obj_sheet.GetParameter(l_int_iteratorcols).Name
						End If
					End If
			Next
			l_arr_headers = Split(l_str_headers, ",")
			l_dict_sheetparams.Add l_str_sheetname, l_arr_headers
	Next		
			set g_dict_sheetheaders = l_dict_sheetparams
		set func_testdata_TCHeaders = l_dict_sheetparams
End Function

'# ********************************************************************************************
'# Function/Sub Name        : func_testdata_testcases
'# Purpose                  : to get test case details in dictionary
'# Input Parameters         : main sheet name
'# Output Parameters        : dictionary object with all test case details from main sheet
'# Author			        : Kiran Londhe
'# Date Created[dd-mon-yy]  : 16-Jan-2017
'# Modified By				:
'# Date Modified[dd-mon-yy] :
'# ********************************************************************************************

Public Function func_testdata_testcases(l_str_mainsheetname)
Dim l_dict_testcases
Dim l_int_paramct
Dim l_int_iterator
Dim l_arr_sheetparams
Dim l_int_rownumber

Set l_dict_testcases = CreateObject("Scripting.Dictionary")
	l_arr_sheetparams = g_dict_sheetheaders(l_str_mainsheetname)
	l_int_paramct = UBound(l_arr_sheetparams) 
	l_int_rownumber = DataTable.GetSheet(l_str_mainsheetname).GetCurrentRow
	l_dict_testcases.Add "l_int_rowno",l_int_rownumber
	
	For l_int_iterator = 1 To l_int_paramct
		l_dict_testcases.Add l_arr_sheetparams(l_int_iterator - 1),DataTable.Value(l_int_iterator,l_str_mainsheetname)
	Next

Set func_testdata_testcases = l_dict_testcases
End Function


'# ********************************************************************************************
'# Function/Sub Name        : func_testdata_getTCdata
'# Purpose                  : to get test data in dictionary object
'# Input Parameters         : sheet name, Primary key (testID col name),value(testId)
'# Output Parameters        : dictionary object with all test data
'# Author			        : Kiran Londhe
'# Date Created[dd-mon-yy]  : 16-Jan-2017
'# Modified By				:
'# Date Modified[dd-mon-yy] :
'# ********************************************************************************************

Public Function func_testdata_getTCdata(l_str_sheetname,l_str_primarykey,l_str_value)
Dim l_dict_tcdata
Dim l_int_paramct
Dim l_str_paramvalue
Dim l_str_PKvalue
Dim l_int_iterator
Dim l_arr_sheetparams
Dim l_int_rownumber
Dim l_obj_datasheet
Dim l_int_rowct
Dim l_str_header

set l_obj_datasheet = DataTable.GetSheet(l_str_sheetname)
l_int_rowct = l_obj_datasheet.GetRowCount

	For l_int_iterator = 1 To l_int_rowct 'find primary key matching row
		l_str_PKvalue = DataTable.GetSheet(l_str_sheetname).GetParameter(l_str_primarykey).ValueByRow(l_int_iterator)
		If strComp(lcase(trim(l_str_PKvalue)),lcase(trim(l_str_value))) = 0 Then
			l_int_rownumber = l_int_iterator
			Exit For
		End If
	Next
	Set l_dict_tcdata = CreateObject("Scripting.Dictionary")
	l_dict_tcdata("l_int_rowno") = l_int_rownumber
	l_arr_sheetparams = g_dict_sheetheaders(l_str_sheetname)
	l_int_paramct = UBound(l_arr_sheetparams) + 1
	
	For l_int_iterator = 0 To l_int_paramct-1
		l_str_header = l_arr_sheetparams(l_int_iterator)
		l_str_paramvalue = DataTable.GetSheet(l_str_sheetname).GetParameter(l_str_header).ValueByRow(l_int_rownumber)
		l_dict_tcdata.Add l_arr_sheetparams(l_int_iterator),l_str_paramvalue
	Next
	
set func_testdata_getTCdata = l_dict_tcdata
End Function

'# ********************************************************************************************
'# Function/Sub Name        : func_testdata_getdatafrmdict
'# Purpose                  : to get test data from test data dictionary
'# Input Parameters         : dictionary name, column name
'# Output Parameters        : column value
'# Author			        : Kiran Londhe
'# Date Created[dd-mon-yy]  : 16-Jan-2017
'# Modified By				:
'# Date Modified[dd-mon-yy] :
'# ********************************************************************************************

Public Function func_testdata_getdatafrmdict(l_dict_details,l_str_colnname)
Dim l_str_output
l_str_output = ""
	
	If l_dict_details.Exists(l_str_colnname) Then
		l_str_output = l_dict_details(l_str_colnname)
	End If
	
	func_testdata_getdatafrmdict = l_str_output
End Function
